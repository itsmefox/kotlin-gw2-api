apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task coreSourcesJar(type: Jar) {
    archiveClassifier.set('sources')

    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives coreSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact coreSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Simple Kotlin Version of the official Guild Wars 2 API'
                    url = 'https://github.com/nusphere/kotlin-gw2-api'
                    licenses {
                        license {
                            name = 'MIT Licence'
                            url = 'https://github.com/nusphere/kotlin-gw2-api/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'nusphere'
                            name = 'Sebastian Hopfe'
                            email = 'nusphere83@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/nusphere/kotlin-gw2-api.git'
                        developerConnection = 'scm:git:ssh://github.com/nusphere/kotlin-gw2-api.git'
                        url = 'https://github.com/nusphere/kotlin-gw2-api/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )

    sign publishing.publications
}